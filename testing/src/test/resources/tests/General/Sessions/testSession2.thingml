import "../../thingml.thingml"

thing TestSession2 includes Test
@test "ABC # (ABC|ACB|BAC|BCA|CAB|CBA)"
{
	property ID : Char = '\'M\''
	
	property finished : Integer = 0

	message m(id : Char)
	message n(id : Char)

	internal port p	{
		sends m, n
		receives m, n
	}

	statechart Chart init INIT {

    	state INIT {
			internal
			event e : Test?In
			action do
				ID = e.c
				fork s
                p!m(ID)
				ID = '\'M\''
			end

			internal
			event e : p?n
			action do
				TestOut(e.id)
				finished++
				if (finished == 3) TestDone()
			end						
    	}

		session s <5> init S1 {
			state S1 {
				transition -> S2
				event e : p?m
				guard e.id == ID
				action p!n(ID)
			}

			final state S2 {
			}
		}
    }
}
